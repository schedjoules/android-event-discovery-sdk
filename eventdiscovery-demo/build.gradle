apply plugin: 'com.android.application'

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
def googlePlacesApiKey = localProperties.getProperty('google.places.api.key')
if (googlePlacesApiKey == null) {
    // use a dummy key instead of throwing an Exception to allow automated build on Travis, Jenkins, Jitpack et al.
    googlePlacesApiKey = "replace-me"
    println("WARNING! Missing Google Places API key in 'local.properties' with key 'google.places.api.key'. Location search won't work.")
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def gitCommitNo = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'master'
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.schedjoules.eventdiscovery.demo"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode gitCommitNo() * 10 // spread the version number so we can insert versions if necessary
        versionName gitVersion()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [googlePlacesApiKey: googlePlacesApiKey]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // add BuildConfig field that tells whether to log requests or not. By default this is disabled for release builds and enabled for debug builds.
            // to enable request logs for release builds add this line to your local.properties:
            // request.log.enabled=true
            buildConfigField "boolean", "LOG_REQUESTS", localProperties.getProperty("request.log.enabled", "false");
        }
        debug {
            minifyEnabled false
            buildConfigField "boolean", "LOG_REQUESTS", localProperties.getProperty("request.log.enabled", "true");
        }
    }
    dataBinding {
        enabled = true
    }
    /*
    TODO !Important! Below was added to fix build error:
    "Execution failed for task com.android.build.api.transform.TransformException: com.android.builder.packaging.DuplicateFileException: Duplicate files copied in APK META-INF/LICENSE"
    See: http://stackoverflow.com/q/33923461/4247460, http://stackoverflow.com/q/37586800/4247460
    Revisit this to see what is the proper solution.
    */
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile project(path: ':eventdiscovery-sdk')
}
